plugins {
    // Apply the java plugin to add support for Java
    id 'java'

    // Apply the application plugin to add support for building a CLI application.
    id 'application'

    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }
}

def arch = System.getProperty('os.arch')
if (arch.equalsIgnoreCase("aarch64")) arch = "arm64"
println "Architecture: $arch"

def osName = System.properties['os.name'].toLowerCase().replaceAll("\\s","")
if (osName.contains("windows")) osName = "windows"
println "osName: $osName"

def osTarget = System.properties['os.target'] ?: "$osName-$arch"
println "osTarget: $osTarget"

dependencies {
    // This dependency is used by the application.
    implementation 'com.google.guava:guava:32.0.0-android'

    // Common
    implementation group: 'org.bytedeco', name: 'opencv', version: '4.9.0-1.5.10'
    implementation group: 'org.bytedeco', name: 'openblas', version: '0.3.26-1.5.10'

    implementation group: 'org.bytedeco', name: 'opencv', version: '4.9.0-1.5.10', classifier: "${osTarget}"
    implementation group: 'org.bytedeco', name: 'openblas', version: '0.3.26-1.5.10', classifier: "${osTarget}"

    implementation(group: 'org.bytedeco', name: 'javacv', version: '1.5.10') {
        exclude module: 'ffmpeg'
        exclude module: 'flycapture'
        exclude module: 'libdc1394'
        exclude module: 'libfreenect'
        exclude module: 'libfreenect2'
        exclude module: 'librealsense'
        exclude module: 'librealsense2'
        exclude module: 'videoinput'
        exclude module: 'artoolkitplus'
        exclude module: 'flandmark'
        exclude module: 'leptonica'
        exclude module: 'tesseract'
    }

    // db
    implementation 'com.h2database:h2:2.2.224'

    // Use JUnit Jupiter API for testing.
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'

    // Use JUnit Jupiter Engine for testing.
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

application {
    // Define the main class for the application.
    mainClassName = 'opencv.photo.db.App'
}

shadowJar {
    archiveBaseName = "opencv-photo-db-${osTarget}"
    archiveVersion = ''
    archiveClassifier = ''
    mainClassName = 'opencv.photo.db.App'
}

test {
    // Use junit platform for unit tests
    useJUnitPlatform()
}
